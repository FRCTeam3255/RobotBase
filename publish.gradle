apply plugin: 'maven-publish'

ext.licenseFile = files("$rootDir/LICENSE.txt")

def pubVersion = '0.0.2'
def outputsFolder = file("$buildDir/allOutputs")
def versionFile = file("$outputsFolder/version.txt")
def artifactGroupId = 'com.frcteam3255'
def baseArtifactId = 'supercore'
def javaBaseName = "_GROUP_com_vendor_frc_ID_${baseArtifactId}_CLS"
def vendorFileInput = file("src/generate/SuperCORE.json")
def releasesRepoUrl = "releases"
def jsonUrl = "releases/com/frcteam3255/${baseArtifactId}"

task libraryBuild() {}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task outputJar(type: Jar, dependsOn: classes) {
    destinationDirectory = outputsFolder
    from sourceSets.main.output
}

task outputSourcesJar(type: Jar, dependsOn: classes) {
    destinationDirectory = outputsFolder
    classifier = 'sources'
    from sourceSets.main.allSource
}

task outputJavadocJar(type: Jar, dependsOn: javadoc) {
    destinationDirectory = outputsFolder
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives outputJar
    archives outputSourcesJar
    archives outputJavadocJar
}

build.dependsOn outputSourcesJar
build.dependsOn outputJavadocJar
build.dependsOn outputJar

libraryBuild.dependsOn build

publishing {
    repositories {
        maven {
            name = "Local"
            url = releasesRepoUrl
        }
    }
}

task cleanReleaseRepo(type: Delete) {
    group = 'supercore'
    delete releasesRepoUrl
    delete jsonUrl
}

// tasks.matching {it != cleanReleaseRepo}.all {it.dependsOn cleanReleaseRepo}

model {
    publishing {
        publications {
            java(MavenPublication) {
                artifact jar
                artifact sourcesJar
                artifact javadocJar

                artifactId = "${baseArtifactId}"
                groupId artifactGroupId
                version pubVersion
            }
        }
    }
}

ext.fileOutput = file("${jsonUrl}/${pubVersion}/SuperCORE-${pubVersion}.json")
ext.fileOutputLatest = file("${jsonUrl}/SuperCORE-latest.json")

task generateVendorJson() {
    description = "Generates the vendor JSON file"
    group = "SuperCORE"

    outputs.file fileOutput
    inputs.file vendorFileInput

    doLast {
        println "Writing version ${pubVersion} to $fileOutput"

        if (fileOutput.exists()) {
            fileOutput.delete()
        }
        def read = vendorFileInput.text.replace('${pubVersion}', pubVersion).replace('${baseArtifactId}', baseArtifactId).replace('${artifactGroupId}', artifactGroupId)
        fileOutput.write(read)

        if (fileOutputLatest.exists()) {
            fileOutputLatest.delete()
        }
        fileOutputLatest.write(read)
    }
}

task createWebFolders(type: Exec) {
    group = 'supercore'
    executable "sh"
    args "-c","source create_dir.sh"
}

tasks.register('publishSuperCORE') {
    group = 'supercore'
    description = 'Publishes all Maven publications to the external Maven repository.'
    dependsOn clean
    dependsOn tasks.withType(PublishToMavenRepository).matching {
        it.repository == publishing.repositories.Local
    }
    dependsOn tasks.generateVendorJson
    finalizedBy tasks.createWebFolders
}

tasks.register('cleanPublishSuperCORE') {
    group = 'supercore'
    description = 'Cleans releases and publishes all Maven publications to the external Maven repository.'
    dependsOn tasks.cleanReleaseRepo
    dependsOn tasks.publishSuperCORE
}
