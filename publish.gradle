apply plugin: 'maven-publish'

def pubVersion = '0.0.2'
def releasesRepoUrl = "releases"
def vendorFileInput = file("src/generate/SuperCORE.json")
def groupName = 'frcteam3255'
def groupTLD = 'com'

// ========== Auto Gen Variables ========== //
def baseArtifactId = "${rootProject.name.toLowerCase()}"
def artifactGroupId = "${groupTLD}.${groupName}"
def jsonUrl = "${releasesRepoUrl}/${groupTLD}/${groupName}/${baseArtifactId}"


// ========== Building ========== //
task libraryBuild() {}

task sourcesJar(type: Jar, dependsOn: classes) {
    group='building'
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    group='building'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

libraryBuild.dependsOn build

publishing {
    repositories {
        maven {
            name = "Local"
            url = releasesRepoUrl
        }
    }
}

task cleanReleaseRepo(type: Delete) {
    group = 'supercore'
    delete releasesRepoUrl
    delete jsonUrl
}

// tasks.matching {it != cleanReleaseRepo}.all {it.dependsOn cleanReleaseRepo}

model {
    publishing {
        publications {
            java(MavenPublication) {
                artifact jar
                artifact sourcesJar
                artifact javadocJar

                artifactId = "${baseArtifactId}"
                groupId artifactGroupId
                version pubVersion
            }
        }
    }
}


// ========== Publishing ========== //
ext.fileOutput = file("${jsonUrl}/${pubVersion}/${rootProject.name}-${pubVersion}.json")
ext.fileOutputLatest = file("${jsonUrl}/${rootProject.name}-latest.json")

task generateVendorJson() {
    description = "Generates the WPILib vendor JSON file"
    group = "supercore"

    outputs.file fileOutput
    inputs.file vendorFileInput

    doLast {
        println "Writing version ${pubVersion} to $fileOutput"

        if (fileOutput.exists()) {
            fileOutput.delete()
        }
        def read = vendorFileInput.text.replace('${pubVersion}', pubVersion).replace('${baseArtifactId}', baseArtifactId).replace('${artifactGroupId}', artifactGroupId)
        fileOutput.write(read)

        if (fileOutputLatest.exists()) {
            fileOutputLatest.delete()
        }
        fileOutputLatest.write(read)
    }
}

task createWebFolders(type: Exec) {
    group = 'supercore'
    description = 'Creates index.html files to make releases navigatable.'
    executable "sh"
    args "-c","source create_dir.sh"
}

tasks.register('publishSuperCORE') {
    group = 'supercore'
    description = 'Publishes all Maven publications to the listed Maven repository.'
    dependsOn clean
    dependsOn tasks.withType(PublishToMavenRepository).matching {
        it.repository == publishing.repositories.Local
    }
    dependsOn tasks.generateVendorJson
    finalizedBy tasks.createWebFolders
}

tasks.register('cleanPublishSuperCORE') {
    group = 'supercore'
    description = 'Cleans releases and publishes all Maven publications to the listed Maven repository.'
    dependsOn tasks.cleanReleaseRepo
    dependsOn tasks.publishSuperCORE
}
